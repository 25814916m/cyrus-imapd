dnl Process this file with autoconf to produce a configure script.
AC_INIT(imap/imapd.c)
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_SET_MAKE
AC_AIX
dnl AC_MINIX Don't think MINIX handles long file names
AC_ISC_POSIX
AC_PROG_LEX
case "$LEX" in
	flex*) LEXFLAGS="-l";;
	*) LEXFLAGS="";;
esac
AC_SUBST(LEXFLAGS)
AC_CONST
AC_LONG_FILE_NAMES
case "$DEFS" in
	*-DHAVE_LONG_FILE_NAMES*) ;;
	*) echo "The Cyrus IMAPD requires support for long file names"
	   exit 1;;
esac
AC_HAVE_HEADERS(unistd.h)
AC_REPLACE_FUNCS(bcopy strcasecmp ftruncate getdtablesize)
AC_DIR_HEADER
dnl AC_XENIX_DIR  Don't think Xenix handles long file names
AC_SUBST(CPPFLAGS)
AC_SUBST(EXTRA_SUBDIRS)
AC_SUBST(NODEP_LIBS)
echo checking for modern syslog
AC_TEST_CPP([#include <syslog.h>
#ifndef LOG_LOCAL6
#include </nonexistent>
#endif], ,EXTRA_SUBDIRS="${EXTRA_SUBDIRS} syslog" LIBS="${LIBS} ../syslog/libsyslog.a" CPPFLAGS="$CPPFLAGS -I\$(srcdir)/../syslog")
echo checking setproctitle
AC_TEST_CPP([#if defined(_AIX3) || defined(DGUX) || defined(_SEQUENT_) || defined(apollo)
#include </nonexistent>
#endif],AC_DEFINE(SETPROCTITLE))
AC_COMPILE_CHECK(nonblocking fcntl,[#include <sys/types.h>
#include <sys/file.h>
#include <fcntl.h>
#ifndef	FNDELAY
#define FNDELAY		O_NDELAY
#endif],[fcntl(0, F_GETFL, 0)&FNDELAY],WITH_NONBLOCK=fcntl,WITH_NONBLOCK=ioctl)
AC_SUBST(WITH_NONBLOCK)
AC_COMPILE_CHECK(tm_gmtoff,[#include <time.h>],[struct tm tm;
tm.tm_gmtoff = 0;],WITH_GMTOFF=tm,WITH_GMTOFF=gmtime)
AC_SUBST(WITH_GMTOFF)
AC_MMAP
case "$DEFS" in
	*-DHAVE_MMAP*) WITH_MAP="mmap" ;;
	*) echo "*** This system does not have a working mmap()"
	   echo "*** Expect a considerable performance penalty"
	   WITH_MAP="nommap" ;;
esac
AC_SUBST(WITH_MAP)
AC_WITH(lock,WITH_LOCK="$withval",
	AC_FUNC_CHECK(flock,WITH_LOCK="flock",WITH_LOCK="fcntl"))
AC_SUBST(WITH_LOCK)
echo checking for Kerberos
if test -f /etc/krb.conf; then have_kerberos=1; fi
AC_WITH(login,WITH_LOGIN="$withval",
	if test -n "$have_kerberos"; then
		echo using Kerberos authentication
		WITH_LOGIN=krb
	else
		echo using unix /etc/passwd authentication
		WITH_LOGIN=unix
	fi)
AC_SUBST(WITH_LOGIN)
AC_WITH(auth,[WITH_AUTH="$withval"
	case "$withval" in
	${WITH_LOGIN}*) true;;
	*) echo Authorization module incompatibile with login module; exit 1;;
	esac],WITH_AUTH="$WITH_LOGIN")
AC_SUBST(WITH_AUTH)
dnl
dnl XXX MUST CHECK FOR TCL BEFORE KERBEROS V4 XXX
dnl This is because some genius at MIT named one of the Kerberos v4
dnl library functions log().  This of course conflicts with the
dnl logarithm function in the standard math library, used by Tcl.
dnl
dnl Checking for Tcl first puts -lm before -lkrb on the library list.
dnl
TCL_SUBDIRS="cyradm"
AC_WITH(tcl,with_tcl="${withval}")
if test -z "$with_tcl"; then
     if test -f /usr/local/lib/libtcl.a; then
	with_tcl="/usr/local"
     fi
fi
case "$with_tcl" in
	no) TCL_SUBDIRS="" ;;
	"") AC_HAVE_LIBRARY(tcl,NODEP_LIBS="${NODEP_LIBS} -ltcl -lm",
	    [echo "Unable to find Tcl, will not compile cyradm"
	     TCL_SUBDIRS=""]) ;;
	*)  CPPFLAGS="${CPPFLAGS} -I${with_tcl}/include"
	    NODEP_LIBS="${NODEP_LIBS} ${with_tcl}/lib/libtcl.a -lm" ;;
esac
AC_SUBST(TCL_SUBDIRS)
dnl
dnl Test for Kerberos
dnl
AC_WITH(krb,with_krb="${withval}")
if test -z "$with_krb"; then
     if test -f /usr/local/lib/libkrb.a; then
	with_krb="/usr/local"
     fi
fi
case "$with_krb" in
	no) true;;
	"") AC_HAVE_LIBRARY(krb,NODEP_LIBS="${NODEP_LIBS} -lkrb -ldes",
		with_krb="no");;
	*)  CPPFLAGS="${CPPFLAGS} -I${with_krb}/include"
	    NODEP_LIBS="${NODEP_LIBS} ${with_krb}/lib/libkrb.a ${with_krb}/lib/libdes.a";;
esac
if test "$with_krb" = "no"; then
     if test "$WITH_LOGIN" = "krb"; then
	echo Unable to find Kerberos libraries; exit 1
     fi
else
     AC_DEFINE(HAVE_ACTE_KRB)
     LIBOBJS="${LIBOBJS} acte_krb.o";
fi     
dnl
dnl Look for inn
dnl
AC_WITH(inn,INNSHELLVARS="$withval/innshellvars",[
echo checking for inn
if test -f /usr/local/news/innshellvars; then
	INNSHELLVARS=/usr/local/news/innshellvars
elif test -f /usr/lib/news/innshellvars; then
	INNSHELLVARS=/usr/lib/news/innshellvars
elif test -f /usr/news/innshellvars; then
	INNSHELLVARS=/usr/news/innshellvars
else
	echo "did not find inn, don't worry"
	INNSHELLVARS=/did/not/find/innshellvars
fi])
AC_SUBST(INNSHELLVARS)
AC_OUTPUT(Makefile syslog/Makefile et/Makefile lib/Makefile imap/Makefile cyradm/Makefile imtest/Makefile glue/feedcyrus)
